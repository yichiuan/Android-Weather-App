apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.getkeepsafe.dexcount'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.yichiuan.weatherapp"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        buildConfigField 'String', 'OPENWEATHERMAP_API_KEY', "\"${OpenWeatherMap_Api_Key}\""
    }
    signingConfigs {
        releaseConfig
    }
    buildTypes {
        debug {
            minifyEnabled true
            // Uses new built-in shrinker http://tools.android.com/tech-docs/new-build-system/built-in-shrinker
            useProguard false

            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        staging.initWith(buildTypes.debug)
        staging {
            //使得 Proguard 有作用
            debuggable false
            useProguard true
            applicationIdSuffix '.staging'
            versionNameSuffix '-staging'
        }
        release {
            minifyEnabled true
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //Use Proguard optimization
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
        }
    }

    // If you need to add more flavors, consider using flavor dimensions.
    productFlavors {
        mock {
            applicationIdSuffix = ".mock"
        }
        prod {

        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if(variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true);
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
if (keystorePropertiesFile.exists()) {
    def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

    if (keystoreProperties.containsKey('storeFile') && keystoreProperties.containsKey('storePassword') &&
            keystoreProperties.containsKey('keyAlias') && keystoreProperties.containsKey('keyPassword')) {
        android.signingConfigs.releaseConfig.storeFile = file(keystoreProperties['storeFile'])
        android.signingConfigs.releaseConfig.storePassword = keystoreProperties['storePassword']
        android.signingConfigs.releaseConfig.keyAlias = keystoreProperties['keyAlias']
        android.signingConfigs.releaseConfig.keyPassword = keystoreProperties['keyPassword']
    } else {
        android.buildTypes.release.signingConfig = null
    }
} else {
    android.buildTypes.release.signingConfig = null
}

apt {
    arguments {
        eventBusIndex "com.yichiuan.weatherapp.EventBusIndex"
    }
}

dexcount {
    format = "yaml"
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:appcompat-v7:${libraries.supportVersion}"
    compile "com.android.support:design:${libraries.supportVersion}"
    compile "com.android.support.constraint:constraint-layout:${libraries.constraint_layout}"

    compile "com.jakewharton.timber:timber:${libraries.timber}"

    compile "com.google.code.gson:gson:${libraries.gson}"
    apt "com.ryanharter.auto.value:auto-value-gson:${libraries.auto_value_gson}"
    provided "com.ryanharter.auto.value:auto-value-gson:${libraries.auto_value_gson}"

    // EventBus
    compile "org.greenrobot:eventbus:${libraries.eventbus}"
    apt "org.greenrobot:eventbus-annotation-processor:${libraries.eventbus_annotation_processor}"

    // ButterKnife
    compile "com.jakewharton:butterknife:${libraries.butterknife}"
    apt "com.jakewharton:butterknife-compiler:${libraries.butterknife}"

    // Retrofit
    compile "com.squareup.retrofit2:retrofit:${libraries.retrofit}"
    compile "com.squareup.retrofit2:converter-gson:${libraries.retrofit}"
    compile "com.squareup.retrofit2:adapter-rxjava:${libraries.retrofit}"
    compile "com.squareup.okhttp3:okhttp:${libraries.okhttp3}"
    compile "com.squareup.okhttp3:logging-interceptor:${libraries.okhttp3}"

    // RxJava
    compile "io.reactivex:rxandroid:${libraries.rxandroid}"
    compile "io.reactivex:rxjava:${libraries.rxjava}"

    compile "com.tbruyelle.rxpermissions:rxpermissions:${libraries.rxpermissions}"

    // LeakCanary
    debugCompile "com.squareup.leakcanary:leakcanary-android:${libraries.leakcanary}"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:${libraries.leakcanary}"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:${libraries.leakcanary}"

    // Stetho
    debugCompile "com.facebook.stetho:stetho:${libraries.stetho}"
    debugCompile "com.facebook.stetho:stetho-okhttp3:${libraries.stetho}"

    // Dependencies for local unit tests
    testCompile "junit:junit:${libraries.junit}"
    testCompile "org.mockito:mockito-core:${libraries.mockito}"
    testCompile "org.hamcrest:hamcrest-all:${libraries.hamcrest}"
    testCompile "org.assertj:assertj-core:${libraries.assertj}"

    // Android Testing Support Library
    androidTestCompile "com.android.support.test:runner:${libraries.testrunner}"
    androidTestCompile "com.android.support.test:rules:${libraries.testrunner}"
    androidTestCompile "com.android.support.test.espresso:espresso-core:${libraries.espresso}"
    androidTestCompile ("com.android.support.test.espresso:espresso-contrib:${libraries.espresso}") {
        exclude module: 'recyclerview-v7'
        exclude module: 'design'
        exclude module: 'support-v4'
    }
    androidTestCompile "com.android.support.test.espresso:espresso-idling-resource:${libraries.espresso}"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:${libraries.espresso}"

    androidTestCompile "com.github.blueapron:dexmaker:${libraries.blueapron_dexmaker}"
}

/*
Resolves dependency versions across test and production APKs, specifically, transitive
dependencies. This is required since Espresso internally has a dependency on support-annotations.
*/
configurations.all {
    resolutionStrategy.force "com.android.support:support-annotations:${libraries.supportVersion}"
}